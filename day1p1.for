      PROGRAM DAY1P1
C
C     CODE IS FOR MICROSOFT FORTRAN-80 V3.44 UNDER CP/M 2.2
C     ENSURE FIRST LINE OF INPUT FILE IS BLANK IF CREATED
C     WITH ED AND DATA COPY/PASTED FROM LINUX BROWSER WINDOW!!!
C
      INTEGER DIAL,NEWVAL,BPTR,TOTAL
      DOUBLE PRECISION LINE
      INTEGER*1 BUFFER(8)
C
      EQUIVALENCE(BUFFER(1),LINE)
C
C     BUFFER(1) WILL BE A LINE FEED, DATA STARTS AT
C     BUFFER(2) - L/R ROTATION FLAG. INPUT IS SPACE
C     PADDED (ASCII 32) AT THE END OF BUFFER ARRAY.
C
   10 FORMAT(34H ADVENT OF CODE 2025 DAY 1, PART 1/1X)
   12 FORMAT(25H ERROR READING INPUT FILE)
   20 FORMAT(A8)
   30 FORMAT(18H ZERO IS INDICATED,I4,6H TIMES)
C
      WRITE(5,10)
      CALL OPEN(10,'DAY1IN  TXT',0)
C     DIAL STARTS AT 50, COUNT TIMES IT READS 0
      DIAL=50
      TOTAL=0
   50 CONTINUE
      READ(10,20,ERR=100,END=200)LINE
C     NUMERIC VALUE IS IN BUFFER(3) ONWARDS
      NEWVAL=0
      DO 60 BPTR=3,8
C       FINISH LOOP IF WE GET A SPACE
        IF (BUFFER(BPTR).EQ.32) GOTO 70
        NEWVAL=NEWVAL*10+(BUFFER(BPTR)-48)
   60 CONTINUE
   70 CONTINUE
C     ASCII L IS 76, R IS 82
      IF (BUFFER(2).EQ.76) NEWVAL=-NEWVAL
      DIAL=MOD(DIAL+NEWVAL,100)
      IF (DIAL.EQ.0) TOTAL=TOTAL+1
      GOTO 50
  100 CONTINUE
      WRITE(5,12)
      ENDFILE 10
      STOP 8
  200 CONTINUE
      ENDFILE 10
      WRITE(5,30)TOTAL
      END
